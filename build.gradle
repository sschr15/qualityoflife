buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        maven { url 'https://repo.spongepowered.org/maven/' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
    }
}

plugins {
    id 'maven-publish'
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'

group = 'sschr15'
version = '1.1.0'
//noinspection GroovyUnusedAssignment
archivesBaseName = 'qol'

//noinspection GroovyUnusedAssignment
sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'

mixin {
    add sourceSets.main, 'qol.mixins.refmap.json'
}

repositories {
    maven { url 'https://repo.spongepowered.org/maven' }
    maven { url 'https://www.cursemaven.com' }
    maven { url 'https://modmaven.k-4u.nl' }
    maven { url 'https://jitpack.io' }
    maven {
        url 'http://files.minecraftforge.net/maven/'
        name 'MinecraftForge maven because Forgegradle is dumb'
    }
    mavenCentral()
}

minecraft {
    mappings channel: 'stable', version: '39-1.12'

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            property 'fml.coreMods.load', 'sschr15.qol.util.mixin.MixinSetupCoremod'
            property 'mixin.hotSwap', 'true'
            property 'mixin.checks.interfaces', 'true'

            mods {
                qualityoflife {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'SCAN,REGISTRIES,REGISTRYDUMP'

            property 'forge.logging.console.level', 'debug'

            property 'fml.coreMods.load', 'sschr15.qol.util.mixin.MixinSetupCoremod'
            property 'mixin.hotSwap', 'true'
            property 'mixin.checks.interfaces', 'true'

            mods {
                qualityoflife {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    embed
    implementation.extendsFrom(embed)
}

dependencies {
    implementation 'org.jetbrains:annotations:19.0.0'

    minecraft 'net.minecraftforge:forge:1.12.2-14.23.5.2854'

    embed(group: 'org.spongepowered', name: 'mixin', version: '0.8-SNAPSHOT') {
        transitive(false)
    }
    embed group: 'org.reflections', name: 'reflections', version: '0.9.12'

    compileOnly group: 'curse.maven', name: 'InventoryTweaks', version: '2482483'
}

jar {
    from(configurations.embed.collect {it.isDirectory() ? it : zipTree(it) }) {
        exclude 'LICENSE.txt', 'META-INF/MANIFSET.MF', 'META-INF/maven/**', 'META-INF/*.RSA', 'META-INF/*.SF'
    }

    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'FMLCorePlugin': 'sschr15.qol.util.mixin.MixinSetupCoremod'
    )

    finalizedBy('reobfJar')
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}
